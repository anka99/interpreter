-- programs ------------------------------------------------

entrypoints Program ;

Program.   Program ::= [TopDef] ;

TopDef.	   TopDef ::= Decl ;

separator nonempty TopDef "" ;

-- declarations --------------------------------------------

Decl.      Decl ::= Type [Item] ";" ;

FnDecl.    Decl ::= Type Ident "(" [Arg] ")" Block ;

Arg. 	   Arg ::= Type Ident ;

ArgRef.    Arg ::= Type "&" Ident ;

separator  Arg "," ;

NoInit.    Item ::= Ident ;

Init.      Item ::= Ident "=" Expr ;

Array.     Item ::= Ident "[" Expr "]" ;

separator nonempty Item "," ;

-- statements ----------------------------------------------

Block.     Block ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Block ;

SDecl.     Stmt ::= Decl ;

Ass.       Stmt ::= Ident "=" Expr  ";" ;

AssArr.    Stmt ::= Ident "[" Expr "]" "=" Expr ";" ;

Incr.      Stmt ::= Ident "++"  ";" ;

IncrArr.   Stmt ::= Ident "[" Expr "]" "++" ";" ;

Decr.      Stmt ::= Ident "--"  ";" ;

DecrArr.   Stmt ::= Ident "[" Expr "]" "--"  ";" ;

Ret.       Stmt ::= "return" Expr ";" ;

VRet.      Stmt ::= "return" ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Stmt ;

CondElse.  Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt ;

While.     Stmt ::= "while" "(" Expr ")" Stmt ;

Brk.       Stmt ::= "break" ";" ;

Cnt.       Stmt ::= "continue" ";" ;

SExp.      Stmt ::= Expr  ";" ;

-- Types ---------------------------------------------------

Int.       Type ::= "int" ;

Str.       Type ::= "string" ;

Bool.      Type ::= "bool" ;

Void.      Type ::= "void" ;

internal   Fun. Type ::= Type "(" [Type] ")" ;

separator  Type "," ;

-- Expressions ---------------------------------------------

EArr.      Expr6 ::= Ident "[" Expr "]" ;

EVar.      Expr6 ::= Ident ;

ELitInt.   Expr6 ::= Integer ;

ELitTrue.  Expr6 ::= "true" ;

ELitFalse. Expr6 ::= "false" ;

EApp.      Expr6 ::= Ident "(" [Expr] ")" ;

EString.   Expr6 ::= String ;

Neg.       Expr5 ::= "-" Expr6 ;

Not.       Expr5 ::= "!" Expr6 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;

EOr.       Expr ::= Expr1 "||" Expr ;

coercions  Expr 6 ;

separator  Expr "," ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment    "//" ;

comment    "/*" "*/" ;
